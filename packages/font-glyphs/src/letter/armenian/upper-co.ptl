$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"
import [DependentSelector] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Armenian-Upper-Co : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : SerifFrame
	glyph-block-import Letter-Armenian-Shared-Shapes : ArmHBar

	do "C'o"
		create-glyph 'armn/Co' 0x551 : glyph-proc
			local df : include : DivFrame 1
			include : df.markSet.capital
			local midy : CAP * HBarPos
			local midyTop : midy + 0.5 * df.mvs
			local midyBot : midy - 0.5 * df.mvs
			include : [ArmHBar.left df 1].at midy
			include : dispiro
				widths.lhs df.mvs
				flat df.leftSB midyBot
				curl df.middle midyBot
				archv
				flatside.ru df.rightSB midyBot CAP ArchDepthA ArchDepthB 0
				arch.lhs CAP (sw -- df.mvs)
				flatside.ld df.leftSB midyBot CAP ArchDepthA ArchDepthB 0
				arcvh
				straight.right.end df.middle midyBot [heading Rightward]
			include : dispiro
				widths.rhs df.mvs
				straight.right.start df.middle midyTop [heading Rightward]
				flatside.rd df.rightSB 0 midyTop SmallArchDepthA SmallArchDepthB
				hookend 0 (sw -- df.mvs)
				g4 (df.leftSB + OX) Hook
