$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"

glyph-module

glyph-block Symbol-Math-Letter-Like : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Latin-Upper-A : DeltaShape
	glyph-block-import Letter-Latin-Upper-E : RevEShape

	do "Partial derivative symbol"
		define fine : Stroke * CThin
		define coFine : mix fine Stroke 0.5

		define yMid : Ascender * 0.6
		define yMid2 : mix yMid [YSmoothMidR yMid 0 SmallArchDepthA SmallArchDepthB] 0.875

		define [ArcStartKnots] : list
			g4.right.start (Middle + CorrectionOMidS - TanSlope * (Stroke - fine)) (O + Stroke - fine + 1/16) [widths.lhs fine]
			straight.up.mid (RightSB - OX - (1 - CThin) * Stroke * HVContrast) [YSmoothMidR yMid 0 SmallArchDepthA SmallArchDepthB]
			arcvh
			g4 (Middle - CorrectionOMidS) (yMid - O) [widths.lhs]
			archv
			g4 (SB + OX) [YSmoothMidL yMid 0 SmallArchDepthA SmallArchDepthB]
			arcvh
			g4 (Middle + CorrectionOMidS) O
			archv
			flat (RightSB - OX) [YSmoothMidR yMid 0 SmallArchDepthA SmallArchDepthB] [widths.lhs coFine]
			curl (RightSB - OX) yMid2

		create-glyph 'partial.curly-bar' : glyph-proc
			include : MarkSet.b
			include : dispiro
				ArcStartKnots
				quadControls 0 0.8
				g4 (SB + Stroke * 1.1) Ascender

		create-glyph 'partial.straight-bar' : glyph-proc
			include : MarkSet.b
			local yMockBarStart : yMid2 + Stroke * 0.3
			local xTerminal : [mix SB RightSB 0.4] + 0.5 * Stroke * HVContrast
			local pStraightBarStart : 0.75 - (Stroke / Ascender)
			include : intersection [MaskBelow Ascender] : dispiro
				ArcStartKnots
				flat [mix Width xTerminal pStraightBarStart] [mix yMockBarStart CAP pStraightBarStart] [widths.lhs]
				curl [mix Width xTerminal 2]                 [mix yMockBarStart CAP 2]

		select-variant 'partial' 0x2202

	turned 'forall' 0x2200 'A.straightSerifless' Middle (CAP / 2)
	alias 'exists' 0x2203 'revE.serifless'
	turned 'amalg' 0x2A3F 'grek/Pi' Middle (CAP / 2)

	create-glyph 'emptyset' 0x2205 : glyph-proc
		include : OShape CAP 0 SB RightSB OperatorStroke
		include : MarkSet.capital
		local fine : OperatorStroke / 2
		include : dispiro
			widths.center OperatorStroke
			flat (SB + O + fine) [mix CAP 0 1.05]
			curl (RightSB - O - fine) [mix 0 CAP 1.05]

	create-glyph 'reverseEmptyset' 0x29B0 : glyph-proc
		include : OShape CAP 0 SB RightSB OperatorStroke
		include : MarkSet.capital
		local fine : OperatorStroke / 2
		include : dispiro
			widths.center OperatorStroke
			flat (RightSB - O - fine) [mix CAP 0 1.05]
			curl (SB + O + fine) [mix 0 CAP 1.05]

	derive-composites 'emptysetOverbar'    0x29B1 'emptyset' 'sbRsbOverlineAbove'
	derive-composites 'emptysetRing'       0x29B2 'emptyset' 'ringAbove'
	derive-composites 'emptysetRightArrow' 0x29B3 'emptyset' 'rightArrowAbove'
	derive-composites 'emptysetLeftArrow'  0x29B4 'emptyset' 'leftArrowAbove'

	define [CodeNonApl u] : if (para.variantSelector.__enableAplForm === 'enable') null u
	define [CodeApl    u] : if (para.variantSelector.__enableAplForm === 'enable') u null

	create-glyph 'increment' [CodeNonApl 0x2206] : glyph-proc
		include : MarkSet.capital
		include : DeltaShape
			df -- [DivFrame 1]
			fBarStraight -- true
			top -- CAP
			sw -- OperatorStroke

	create-glyph 'increment.aplForm' [CodeApl 0x2206] : glyph-proc
		include : MarkSet.oper
		define shapeHeight : OperTop - OperBot
		include : DeltaShape
			df -- [DivFrame 1]
			fBarStraight -- true
			top -- shapeHeight
			sw -- OperatorStroke
		include : ApparentTranslate 0 (SymbolMid - shapeHeight / 2)

	create-glyph 'increment.aplThin' : glyph-proc
		include : MarkSet.oper
		define shapeHeight : OperTop - OperBot
		include : DeltaShape
			df -- [DivFrame 1]
			fBarStraight -- true
			top -- shapeHeight
			sw -- [AdviceStroke 3.75]
		include : ApparentTranslate 0 (SymbolMid - shapeHeight / 2)

	turned 'nabla' [CodeNonApl 0x2207] 'increment' Middle (CAP / 2)
	turned 'nabla.aplForm' [CodeApl 0x2207] 'increment.aplForm' Middle SymbolMid
	turned 'nabla.aplThin' null 'increment.aplThin' Middle SymbolMid
